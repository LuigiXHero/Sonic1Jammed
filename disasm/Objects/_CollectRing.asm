; ---------------------------------------------------------------------------
; Subroutine to	update ring counters and lives when a ring is collected

;	uses d0
; ---------------------------------------------------------------------------

CollectRing:
		addq.w	#1,(v_rings).w				; add 1 to rings
		ori.b	#1,(v_hud_rings_update).w		; update the rings counter
		move.w	#sfx_Ring,d0				; play ring sound
		cmpi.w	#100,(v_rings).w			; do you have < 100 rings?
		bcs.s	@playsnd				; if yes, branch
		bset	#1,(v_ring_reward).w			; remember 100 rings have been collected
		beq.s	@got100					; branch if 100 rings haven't been collected previously
		cmpi.w	#200,(v_rings).w			; do you have < 200 rings?
		bcs.s	@playsnd				; if yes, branch
		bset	#2,(v_ring_reward).w			; remember 200 rings have been collected
		bne.s	@playsnd				; branch if 200 rings have been collected previously

	@got100:
		addq.b	#1,(v_lives).w				; add 1 to the number of lives you have
		addq.b	#1,(f_hud_lives_update).w		; update the lives counter
		move.w	#mus_ExtraLife,d0			; play extra life music

	@playsnd:
		jmp	(PlaySound1).l
